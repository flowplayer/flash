<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   
			   minWidth="955" 
			   minHeight="600"
			   
			   initialize="onApplicationInitialize(event)"
			   applicationComplete="onApplicationComplete(event)">
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.events.IndexChangedEvent;
			
			import org.osmf.containers.MediaContainer;
			import org.osmf.elements.DurationElement;
			import org.osmf.events.AlternativeAudioEvent;
			import org.osmf.events.BufferEvent;
			import org.osmf.events.MediaErrorEvent;
			import org.osmf.events.MediaPlayerCapabilityChangeEvent;
			import org.osmf.events.MediaPlayerStateChangeEvent;
			import org.osmf.events.TimeEvent;
			import org.osmf.layout.HorizontalAlign;
			import org.osmf.layout.LayoutMetadata;
			import org.osmf.layout.LayoutMode;
			import org.osmf.layout.ScaleMode;
			import org.osmf.layout.VerticalAlign;
			import org.osmf.media.DefaultMediaFactory;
			import org.osmf.media.MediaElement;
			import org.osmf.media.MediaFactory;
			import org.osmf.media.MediaPlayer;
			import org.osmf.media.MediaPlayerState;
			import org.osmf.media.URLResource;
			import org.osmf.metadata.MetadataNamespaces;
			import org.osmf.net.DynamicStreamingItem;
			import org.osmf.net.StreamingItem;
			import org.osmf.utils.OSMFSettings;
			
			import spark.components.supportClasses.DropDownController;
			import spark.events.IndexChangeEvent;
			
			/**
			 * @private
			 * We just create the initial collection of resources.
			 */
			protected function onApplicationInitialize(event:FlexEvent):void
			{
				urls = new ArrayCollection(URLS);
			}
			
			/**
			 * @private
			 * We'll create all our application objects.
			 **/
			protected function onApplicationComplete(event:FlexEvent):void
			{
				OSMFSettings.enableStageVideo = false;
				
				// factory
				factory = new DefaultMediaFactory();
				
				// player
				player = new MediaPlayer();
				player.autoPlay = false;
				player.autoRewind = false;
				player.addEventListener(MediaPlayerStateChangeEvent.MEDIA_PLAYER_STATE_CHANGE, onPlayerStateChange);
				player.addEventListener(MediaPlayerCapabilityChangeEvent.CAN_PLAY_CHANGE, onPlayerCanPlayChange);
				player.addEventListener(TimeEvent.DURATION_CHANGE, onDurationChange);
				player.addEventListener(TimeEvent.CURRENT_TIME_CHANGE, onPlayerCurrentTimeChange);
				player.addEventListener(AlternativeAudioEvent.AUDIO_SWITCHING_CHANGE, onPlayerAudioStreamChange);
				player.addEventListener(MediaErrorEvent.MEDIA_ERROR, onPlayerError);
								
				// container
				container = new MediaContainer();
				container.width = 690;
				container.height = 480;
				videoContainer.addChild(container);
				
				updateUI();
			}
				
			/**
			 * @private
			 * When resources combo box selected index changes, we stop the playback of the current resource
			 * and switch to the new media.
			 **/
			protected function onResourceSelectionChange(event:IndexChangeEvent):void
			{
				var dropDown:DropDownList = event.target as DropDownList;
				if (dropDown != null)
				{
					var selectedIndex:int = dropDown.selectedIndex;
					var selectedUrl:String = null;
					if (dropDown.selectedItem != null && dropDown.selectedItem.hasOwnProperty("data"))
						selectedUrl = dropDown.selectedItem["data"];
					
					trace("[LBA - Sample] Selected index :", selectedIndex);
					trace("[LBA - Sample] Selected url   :", selectedUrl);
					
					unloadResource();
					if (selectedUrl != null)
						createResource(selectedUrl);
				}
			}
			
			/**
			 * @private
			 **/
			protected function onLanguageSelectionChange(event:IndexChangeEvent):void
			{
				var dropDown:DropDownList = event.target as DropDownList;
				if (dropDown != null)
				{
					var selectedItem:Object = dropDown.selectedItem;
					if (selectedItem != null)
					{
						alternativeLanguage = selectedItem.data;
						trace("[LBA - Sample] Selected language index :", alternativeLanguage);
						trace("[LBA - Sample] Selected language :", dropDown.selectedItem["label"]);
						
						if (player.hasAlternativeAudio)
						{
							player.switchAlternativeAudioIndex(alternativeLanguage);
						}
					}
				}
			}
			
			protected function onStreamSelectionChange(event:IndexChangeEvent):void
			{
				var dropDown:DropDownList = event.target as DropDownList;
				if (dropDown != null)
				{
					if (player.isDynamicStream)
					{
						var selectedIndex:int = dropDown.selectedIndex;
						trace("[LBA - Sample] Changing bitrate index to ", selectedIndex);
						player.switchDynamicStreamIndex(selectedIndex);
					}
				}				
			}			
			
			/**
			 * @private
			 * Clears existing resource.
			 **/			
			private function unloadResource():void
			{
				if (player.canPlay && player.state == MediaPlayerState.PLAYING)
					player.stop();
				
				player.media = null;
			}
			
			/**
			 * @private
			 * Create a resource from the specified url.
			 **/
			private function createResource(url:String):void
			{
				var element:MediaElement = factory.createMediaElement(new URLResource(url));
				
				var elementLayout:LayoutMetadata = new LayoutMetadata();
				elementLayout.percentHeight = 100;
				elementLayout.percentWidth = 100;
				elementLayout.scaleMode = ScaleMode.LETTERBOX;
				elementLayout.layoutMode = LayoutMode.NONE;
				elementLayout.verticalAlign = VerticalAlign.MIDDLE;
				elementLayout.horizontalAlign = HorizontalAlign.CENTER;
				element.addMetadata(LayoutMetadata.LAYOUT_NAMESPACE, elementLayout);
				
				container.addMediaElement(element);
				player.media = element;
			}
			
			/**
			 * @private
			 * Updates UI elements to reflect the player states.
			 **/
			private function updateUI():void
			{
				btnPlayPause.enabled = player.canPlay;
				listLanguages.enabled = (player.state != MediaPlayerState.PLAYBACK_ERROR && player.state != MediaPlayerState.UNINITIALIZED && player.hasAlternativeAudio);
				chkAutoRewind.selected = player.autoRewind;
				chkAutoRewind.enabled = (player.state != MediaPlayerState.PLAYBACK_ERROR && player.state != MediaPlayerState.UNINITIALIZED);
			}
			
			/**
			 * @private
			 * Updates ui element to reflect the new duration.
			 **/
			private function onDurationChange(event:TimeEvent):void
			{
				if (player.temporal)
				{
					sldSeek.maximum = player.duration;
					sldSeek.minimum = 0;
				}
			}
			
			/**
			 * @private
			 * Track when the player is capable to play in order to enable UI.
			 */
			private function onPlayerCanPlayChange(event:MediaPlayerCapabilityChangeEvent):void
			{
				updateUI();
			}
			
			/**
			 * @private
			 * Called when the player current time has changed. We update the slider position.
			 **/
			private function onPlayerCurrentTimeChange(event:TimeEvent):void
			{
				if (event.time >= sldSeek.minimum && event.time <= sldSeek.maximum) 
				{
					sldSeek.value = event.time;
					lblTime.text = Number(event.time).toFixed(3);
				}
			}
			
			/**
			 * @private
			 * When the player is ready for playback, iterate through alternate
			 * audio tracks and update the UI.
			 **/
			private function onPlayerStateChange(event:MediaPlayerStateChangeEvent):void
			{
				updateUI();
				var targetPlayer:MediaPlayer = event.target as MediaPlayer;
				if (targetPlayer != null && event.state == MediaPlayerState.READY)
				{
					languages = new ArrayCollection();
					alternativeLanguage = -1;
					
					var index:int = 0;
					if (targetPlayer.hasAlternativeAudio)
					{
						trace("[LBA - Sample] Alternative audio tracks available.");
						
						var languageItem:Object = new Object();
						languageItem["label"] = "Default";
						languageItem["data"] = -1;
						languages.addItem(languageItem); 
						
						for (index = 0; index < targetPlayer.numAlternativeAudioStreams; index++)
						{
							var item:StreamingItem = targetPlayer.getAlternativeAudioItemAt(index);
							trace("[LBA - Sample] ", item.info.language, "]", item.info.label);
							
							languageItem = new Object();
							languageItem["label"] = item.info.label + "(" + item.info.language + ")";
							languageItem["data"] = index;
							languages.addItem(languageItem); 
						}
					}
					
					streams = new ArrayCollection();
					
					if (targetPlayer.isDynamicStream)
					{
						trace("[LBA - Sample] Dynamic streams available.");
						for (index = 0; index < targetPlayer.numDynamicStreams; index++)
						{
							var bitrate:Number = targetPlayer.getBitrateForDynamicStreamIndex(index);
							trace("[LBA - Sample] Bitrate :", bitrate);
							
							var bitRateItem:Object = new Object();
							bitRateItem["data"] = bitRateItem["label"] = bitrate;
							streams.addItem(bitrate);
						}
					}
				}
			}
			
			/**
			 * @private
			 * Executed when player encounters an error.
			 **/
			private function onPlayerError(event:MediaErrorEvent):void
			{
				updateUI();
			}
			
			/**
			 * @private 
			 * Play the current media element.
			 **/
			private function onPlayPauseClick(event:MouseEvent):void
			{
				if (!player.playing)
				{
					if (player.canPlay)
						player.play();
				}
				else
				{
					if (player.canPause)
						player.pause();
				}
			}
			
			/**
			 * @private 
			 * Snap the current media element to live.
			 **/
			private function onSnapToLiveClick(event:MouseEvent):void
			{
				if (player != null && player.state == MediaPlayerState.PLAYING)
				{
					if (player.canSeek)
					{
						player.seek(10000);
					}
				}
			}

			private function onSeekRequest():void
			{
				var seekTarget:Number = sldSeek.value;	
				if (player.canSeek)
					player.seek(seekTarget);
			}
			
			private function onAutoRewindChange(event:Event):void
			{
				player.autoRewind = chkAutoRewind.selected;
			}
			
			private function onAutoSwitchChange(event:Event):void
			{
				player.autoDynamicStreamSwitch = chkAutoSwitch.selected;
			}
			
			/**
			 * @private
			 * Listen for audio stream events.
			 **/
			private function onPlayerAudioStreamChange(event:AlternativeAudioEvent):void
			{
				if (event.switching)
					trace("[LBA - Sample] Alternative audio stream is switching.");
				else
					trace("[LBA - Sample] Alternative audio switch is complete.");
			}
			
			/**
			 * @private
			 **/
			private var player:MediaPlayer = null;
			private var factory:MediaFactory = null;
			private var container:MediaContainer = null;
			
			private var alternativeLanguage:int = -1;
			private var dynamicStream:int = -1;
			
			private var seekedToLive:Boolean = false;
			
			[Bindable]
			protected var streams:ArrayCollection = null;
			
			[Bindable]
			protected var languages:ArrayCollection = null;
			
			[Bindable]
			protected var urls:ArrayCollection = null;
			private static const URLS:Array = 
						[
//							{
//								label:"Synced video and audio",
//								data:"http://10.131.237.104/hds-live/latebind_sync/_definst_/liveevent.f4m"
//							},
//							{
//								label:"Rolling window",
//								data:"http://10.131.165.62/live/events/livehds/events/_definst_/liveevent.f4m"
//							},
							{
								label:"VOD: 1 video with 2 alternative audio tracks", 
								data:"http://10.131.165.85/vod/vod/late_binding_audio/API_tests_assets/1_media_v_2_alternate_a/1_media_v_2_alternate_a.f4m"	
							},
//							{
//								label:"Seek outside buffer", 
//								data:"http://10.131.237.107/vod/hs/vod/ex.f4m"
//							},
//							{
//								label:"Legacy HDS manifest", 
//								data:"http://zeridemo-f.akamaihd.net/content/inoutedit-mbr/inoutedit_h264_3000.f4m"	
//							},
//							{
//								label:"Legacy Live HDS manifest",
//								data:"http://10.131.237.107/live/videostream.f4m"
//							},
//							{
//								label:"Legacy Live HDS with DVR",
//								data:" http://10.131.237.107/live/events/hs_sbr_dvr/events/_definst_/liveevent.f4m"
//							},
//							{ 
//								label:"Legacy Live HDS manifest with MBR",
//								data:"http://10.131.237.107/live/events/hs_mbr_live/events/_definst_/liveevent.f4m"
//							},
//							{
//								label:"Legacy live HDS manifest without DVR",
//								data:"http://10.122.233.48/live/events/livepkgr/events/_definst_/liveevent.f4m"Â 
//							},
							{
								label:"VOD: 1 audio-video with 2 alternative audio tracks",
								data:"http://10.131.237.104/vod/late_binding_audio/API_tests_assets/1_media_av_2_alternate_a/1_media_av_2_alternate_a.f4m"	
							},
//							{
//								label:"MBR: 1 mbr stream with 2 alternative audio tracks",
//								data:"http://10.131.237.107/vod/mbr/sample1_audio2.f4m"	
//							},
							{
								label:"LIVE: 1 video with 2 alternative audio tracks",
								data:"http://10.131.237.107/live/events/latebind/events/_definst_/liveevent.f4m"
							},
//							{
//								label:"LIVE: 1 video with 1 alternate audio track",
//								data:"http://10.131.237.107/live/purelive_lba.f4m"
//							},
//							{
//								label:"LIVE: audio only",
//								data:"http://10.131.237.107/live/events/live_audio_only/events/_definst_/liveevent_audio_only.f4m"
//							},
//							{	
//								label:"SeeSaw/ioko SBR protected content 1 (700)",
//								data:"http://10.131.237.107/vod/mbr/drm/sample1_drm_700kbps.f4m" //http://10.131.237.107/vod/SeeSaw/35321_single_796.f4m"
//							},
//							{	
//								label:"SeeSaw/ioko SBR protected content 2 (1000)",
//								data:"http://10.131.237.107/vod/mbr/drm/sample1_drm_1000kbps.f4m" //http://10.131.237.107/vod/SeeSaw/35322_single_500.f4m"
//							},
//							{	
//								label:"SeeSaw/ioko SBR protected content 3 (1500)",
//								data:"http://10.131.237.107/vod/mbr/drm/sample1_drm_1500kbps.f4m" //http://10.131.237.107/vod/SeeSaw/35323_single_1500.f4m"
//							},
//							{	
//								label:"SeeSaw/ioko MBR protected content",
//								data:"http://10.131.237.107/vod/mbr/drm/sample1_mbr_drm.f4m" //http://10.131.237.107/vod/SeeSaw/seesaw_mbr.f4m"
//							},
							{
								label:"LIVE: 1 video with 1 synced alternative audio track",
								data:"http://10.131.237.104/hds-live/latebind_sync/_definst_/liveevent.f4m"
							},
							{
								label:"VOD: 1 audio-video with 2 shorter alternative audio tracks",
								data:"http://10.131.237.104/vod/late_binding_audio/API_tests_assets/1_media_av_2_shorter_alternate_a/1_media_av_2_shorter_alternate_a.f4m"
							},
							{
								label:"VOD: 1 video with 2 shorter alternative audio tracks",
								data:"http://10.131.237.104/vod/late_binding_audio/API_tests_assets/1_media_v_2_shorter_alternate_a/interview_video_only_2_shorter_alternate_audio.f4m"
							}
						];

		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<s:Group width="100%" height="100%">
		<s:layout>
			<s:VerticalLayout 
					paddingTop="20"
					horizontalAlign="center" />
		</s:layout>
		
		<s:Panel title="Late-binding audio on HDS sample" >
			<s:layout>
				<s:VerticalLayout 
					gap="5" 
					paddingTop="10" paddingLeft="10" paddingBottom="10" paddingRight="10"
					horizontalAlign="left"
					/>
			</s:layout>
			
			<s:HGroup verticalAlign="middle" height="20" width="100%"  >
				<s:Label text="Please select the desired resource :" />
				<s:DropDownList id="listResources" width="500" dataProvider="{urls}" change="onResourceSelectionChange(event)"/>
			</s:HGroup>
			
			<s:Line width="100%">
				<s:stroke>
					<s:SolidColorStroke color="0x999999"  caps="square" weight="2"/>
				</s:stroke>
			</s:Line>
			
			<s:BorderContainer width="100%" minWidth="640" minHeight="480" backgroundColor="0x000000">
				<s:SpriteVisualElement id="videoContainer" />
			</s:BorderContainer>

			<s:Line width="100%">
				<s:stroke>
					<s:SolidColorStroke color="0x999999"  caps="square" weight="2"/>
				</s:stroke>
			</s:Line>

			<s:Group id="videoMenu" width="100%">
				<s:layout>
					<s:HorizontalLayout 
							gap="5"
							verticalAlign="middle" 
							horizontalAlign="left"
							/>
				</s:layout>
				<s:Button id="btnPlayPause" label="Play" click="onPlayPauseClick(event)" />
				<s:HSlider id="sldSeek" snapInterval="1" width="300" change="onSeekRequest()" />
				<s:Label id="lblTime" width="100" />
				<s:Spacer width="100%" />
				<s:VGroup>
					<s:DropDownList id="listStreams" width="200" dataProvider="{streams}" change="onStreamSelectionChange(event)"/>
					<s:DropDownList id="listLanguages" width="200" dataProvider="{languages}" change="onLanguageSelectionChange(event)"/>
				</s:VGroup>
			</s:Group>
			<s:Group>
				<s:layout>
					<s:VerticalLayout
							gap="5" 
							verticalAlign="middle" 
							horizontalAlign="left"
						/>
				</s:layout>
				<s:CheckBox id="chkAutoRewind" label="autoRewind" change="onAutoRewindChange(event)" />
				<s:CheckBox id="chkAutoSwitch" label="autoSwitch" change="onAutoSwitchChange(event)" />
			</s:Group>
		</s:Panel>
	</s:Group>
</s:Application>
